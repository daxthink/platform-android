apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply from: '../common/quality.gradle'

def globalConfiguration = rootProject.extensions.getByName("ext")

def libVersion = globalConfiguration.getAt("androidVersionName")

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()

def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

def isTravis = "true".equals(System.getenv("TRAVIS"))

def preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))

def getFeedbackEmailAddress() {

    return project.hasProperty('feedbackEmail') ? feedbackEmail : ''
}

// Reads GoogleMap's Debug API key from `./gradle.properties`
def readGoogleMapsDebugApiKey() {
    return project.hasProperty('googleMapsDebugApiKey') ? googleMapsDebugApiKey : ''
}

// Reads GoogleMap's Release API key from `./gradle.properties`
def readGoogleMapsReleaseApiKey() {
    return project.hasProperty('googleMapsReleaseApiKey') ? googleMapsReleaseApiKey : ''
}

android {
    compileSdkVersion globalConfiguration.getAt("androidCompileSdkVersion")
    buildToolsVersion globalConfiguration.getAt("androidBuildToolsVersion")
    defaultConfig {
        applicationId "com.ushahidi.android"
        minSdkVersion globalConfiguration.getAt("androidMinSdkVersion")
        targetSdkVersion globalConfiguration.getAt("androidTargetSdkVersion")
        versionCode globalConfiguration.getAt("androidVersionCode")
        versionName = libVersion
        testInstrumentationRunner globalConfiguration.getAt("testInstrumentationRunner")

        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
        buildConfigField "String", "FEEDBACK_EMAIL_ADDRESS", "\"${getFeedbackEmailAddress()}\""
    }
    buildTypes {
        debug {
            resValue "string", "google_maps_api_key", "\"${readGoogleMapsDebugApiKey()}\""
        }

        release {
            resValue "string", "google_maps_api_key", "\"${readGoogleMapsReleaseApiKey()}\""
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    lintOptions {
        abortOnError false
    }

    productFlavors {
        internal {
            applicationId 'com.ushahidi.android.internal'
        }
    }
}

retrolambda {
    jvmArgs '-noverify'
}

dependencies {
    def appDependencies = rootProject.ext.appDependencies
    def appTestDependencies = rootProject.ext.appTestDependencies

    apt appDependencies.daggerCompiler
    compile appDependencies.raiburari
    compile appDependencies.cupboard
    compile appDependencies.retrofit
    compile appDependencies.okhttp
    compile appDependencies.okhttpUrlConnection
    compile appDependencies.qrCodeReaderView
    compile appDependencies.androidIconifyFontawesome
    compile appDependencies.playServicesMaps
    compile appDependencies.androidMapsUtils
    compile appDependencies.cocoaheroGeoJson
    compile appDependencies.heimdallDroid
    compile appDependencies.stetho
    compile appDependencies.stethoOkHttp
    provided appDependencies.javaxAnnotation

    testCompile appTestDependencies.junit
    testCompile appTestDependencies.mockito
    testCompile appTestDependencies.robolectric
    testCompile appTestDependencies.truth
    testCompile appTestDependencies.retrofitMock
    testCompile appTestDependencies.okhttpMockWebserver
    testApt appDependencies.daggerCompiler

    androidTestCompile appTestDependencies.mockito
    androidTestCompile appTestDependencies.dexmaker
    androidTestCompile appTestDependencies.dexmakerMockito
    androidTestCompile appTestDependencies.espresso
    androidTestCompile appTestDependencies.testingSupportLib
}